cmake_minimum_required(VERSION 3.5)

set(TFSCLIENT_NAME $ENV{TFSCLIENT_NAME})
set(TFSCLIENT_VERSION $ENV{TFSCLIENT_VERSION})
set(TFSCLIENT_VERSION_GRPC $ENV{TFSCLIENT_VERSION_GRPC})
set(TFSCLIENT_VERSION_PROTO $ENV{TFSCLIENT_VERSION_PROTO})

set(CMAKE_BUILD_TYPE $ENV{TFSCLIENT_BUILD})

project(${TFSCLIENT_NAME} VERSION ${TFSCLIENT_VERSION})

find_package(PkgConfig REQUIRED)
pkg_search_module(GRPC REQUIRED grpc)
pkg_search_module(GRPCPP REQUIRED grpc++>=${TFSCLIENT_VERSION_GRPC})
find_package(Protobuf ${TFSCLIENT_VERSION_PROTO} REQUIRED)

set(PROTO_GENERATED "${CMAKE_SOURCE_DIR}/$ENV{TFSCLIENT_SOURCE}")
file(GLOB_RECURSE PROTO_SOURCES "${PROTO_GENERATED}/*.cc")

add_library(${TFSCLIENT_NAME} STATIC ${PROTO_SOURCES})
target_compile_features(${TFSCLIENT_NAME} PUBLIC cxx_std_11)
target_include_directories(${TFSCLIENT_NAME}
  PUBLIC
    $<BUILD_INTERFACE:${PROTO_GENERATED}>
    $<INSTALL_INTERFACE:include>
  PRIVATE
    ${GRPC_INCLUDE_DIRS}
    ${GRPCPP_INCLUDE_DIRS}
    ${PROTOBUF_INCLUDE_DIRS})
target_link_libraries(${TFSCLIENT_NAME}
  PUBLIC
    ${GRPC_LIBRARIES}
    ${GRPCPP_LIBRARIES}
    ${PROTOBUF_LIBRARIES})

include(InstallRequiredSystemLibraries)
set(CPACK_PACKAGE_VERSION ${TFSCLIENT_VERSION})
set(CPACK_PACKAGE_VENDOR $ENV{TFSCLIENT_AUTHOR_NAME})
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY $ENV{TFSCLIENT_DESCRIPTION})
set(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
include(CPack)

set(TFSCLIENT_TARGETS "${TFSCLIENT_NAME}-targets")
set(TFSCLIENT_TARGETF "${TFSCLIENT_TARGETS}.cmake")
set(TFSCLIENT_CONFIGF "${TFSCLIENT_NAME}-config.cmake")
install(TARGETS ${TFSCLIENT_NAME} EXPORT ${TFSCLIENT_TARGETS}
  ARCHIVE DESTINATION lib INCLUDES DESTINATION include)
install(EXPORT ${TFSCLIENT_TARGETS} FILE ${TFSCLIENT_TARGETF} DESTINATION lib/cmake)
install(DIRECTORY "${PROTO_GENERATED}/" DESTINATION include FILES_MATCHING PATTERN "*.h")

include(CMakePackageConfigHelpers)
configure_package_config_file("${CMAKE_CURRENT_SOURCE_DIR}/Config.cmake"
  "${CMAKE_CURRENT_BINARY_DIR}/${TFSCLIENT_CONFIGF}" INSTALL_DESTINATION lib/cmake
  NO_SET_AND_CHECK_MACRO NO_CHECK_REQUIRED_COMPONENTS_MACRO)
install(FILES "${CMAKE_CURRENT_BINARY_DIR}/${TFSCLIENT_CONFIGF}" DESTINATION lib/cmake)

export(EXPORT ${TFSCLIENT_TARGETS} FILE "${CMAKE_CURRENT_BINARY_DIR}/${TFSCLIENT_TARGETF}")
