buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"
    classpath "ru.vyarus:gradle-use-python-plugin:1.2.0"
    classpath "gradle.plugin.com.github.blindpirate:gogradle:0.11.4"
  }
}

apply from: "build.gradle"
apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar) {
  classifier "sources"
  from sourceSets.generated.java
}

task javadocJar(type: Jar) {
  classifier "javadoc"
  from javadoc
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId jar.baseName
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = "Tensorflow Serving Client"
        description = "A client only library for tensorflow serving"
        url = "https://github.com/figroc/tensorflow-serving-client"
        scm {
          url = "https://github.com/figroc/tensorflow-serving-client"
          connection = "scm:git:git://github.com/figroc/tensorflow-serving-client.git"
          developerConnection = "scm:git:ssh://github.com:figroc/tensorflow-serving-client.git"
        }
        licenses {
          license {
            name = "Apache License 2.0"
            url = "https://github.com/figroc/tensorflow-serving-client/blob/master/LICENSE"
          }
        }
        developers {
          developer {
            id = "figroc"
            name = "Figroc Chen"
            email = "figroc@gmail.com"
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
        username sonatypeUsername
        password sonatypePassword
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
