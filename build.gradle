plugins {
  id "java"
  id "maven"
  id "com.google.protobuf" version "0.8.10"
  id "ru.vyarus.use-python" version "1.2.0"
}

ext {
  tfsVer = new File("VERSION").text.trim()
  jvmVer = "8"
  grpcVer = "1.24.0"
  protoVer = "3.10.0"
}

group = "io.opil"
version = "${tfsVer}.j${jvmVer}"

sourceCompatibility = "1.${jvmVer}"
targetCompatibility = "1.${jvmVer}"

repositories {
  mavenCentral()
}

sourceSets {
  main {
    proto {
      srcDir "proto"
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protoVer}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVer}"
    }
  }
  generateProtoTasks {
    all()*.builtins {
      cpp { }
      java { }
      python { }
    }
    all()*.plugins {
      grpc { }
    }
  }
}

dependencies {
  implementation "com.google.protobuf:protobuf-java:${protoVer}"
  implementation "io.grpc:grpc-protobuf:${grpcVer}"
  implementation "io.grpc:grpc-stub:${grpcVer}"
}

jar {
  baseName "tensorflow-serving-client"
}

task wheel(type: PythonTask) {
  dependsOn "generateProto"
  command "setup.py bdist_wheel -d build/dist"
}

task cmake() {
  dependsOn "generateProto"
  doLast {
    exec {
      workingDir "build"
      commandLine "cmake", "-DCMAKE_INSTALL_PREFIX=", ".."
    }
    exec {
      workingDir "build"
      commandLine "make", "DESTDIR=dist", "install"
    }
  }
}
