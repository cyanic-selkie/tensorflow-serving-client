buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"
  }
}

ext {
  jvmVer = "8"
  tfsVer = new File("VERSION").text.trim()
  grpcVer = "1.24.0"
  protoVer = "3.10.0"
}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "com.google.protobuf"

group = "io.opil"
version = "${tfsVer}.j${jvmVer}"

sourceCompatibility = "1.${jvmVer}"
targetCompatibility = "1.${jvmVer}"

repositories {
  mavenCentral()
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protoVer}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVer}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc { }
    }
  }
}

dependencies {
  implementation "com.google.protobuf:protobuf-java:${protoVer}"
  implementation "io.grpc:grpc-protobuf:${grpcVer}"
  implementation "io.grpc:grpc-stub:${grpcVer}"
}

jar {
  baseName "tensorflow-serving-client"
}
