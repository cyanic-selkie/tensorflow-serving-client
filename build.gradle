buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
  }
}

ext {
  jvmV = '8'
  tfsV = new File('VERSION').text.trim()
  grpcV = '1.23.0'
  protoV = '3.9.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.google.protobuf'

group = 'io.opil'
version = "${tfsV}.j${jvmV}"

sourceCompatibility = "1.${jvmV}"
targetCompatibility = "1.${jvmV}"

repositories {
  mavenCentral()
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protoV}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcV}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc { }
    }
  }
}

dependencies {
  compile "com.google.protobuf:protobuf-java:${protoV}"
  compile "io.grpc:grpc-protobuf:${grpcV}"
  compile "io.grpc:grpc-stub:${grpcV}"
}

jar {
  baseName 'tensorflow-serving-client'
}
